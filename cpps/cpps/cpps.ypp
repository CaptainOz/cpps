
%{
    #include <iostream>
    #include <cstdlib>
    #include "cpps.h"
    #include "cpps.tab.hpp"

    int ex( node::Node* nodePtr );
    int cpps_lex( void );

    void cpps_error( YYLTYPE* location, LexerContext* context, const char* message );
    int cpps_lex( YYSTYPE* lval, YYLTYPE* location, void* scanner );

    #define scanner context->scanner

    int symbols[ 'z' - 'a' ];
%}

%pure-parser
%name-prefix="cpps_"
%locations
%defines
%parse-param { LexerContext* context }
%lex-param { void* scanner }

%union {
    int integerValue;
    int symbolIndex;
    node::Node* nodePtr;
};

%token <integerValue> INTEGER
%token <symbolIndex> VARIABLE
%token WHILE IF PRINT
%nonassoc IFX
%nonassoc ELSE

%left GE LE EQ NE '>' '<'
%left '+' '-'
%left '*' '/'
%nonassoc UMINUS

%type <nodePtr> stmt expr stmt_list
%type <integerValue> function

%%

program
    : function          { context->result = $1; }
    ;

function
    : function stmt     { $$ = ex( $2 ); delete $2; }
    |                   { $$ = 0; }
    ;

stmt
    : ';'                               { $$ = new node::Node( ';', 2, NULL, NULL ); }
    | expr ';'                          { $$ = $1; }
    | PRINT expr ';'                    { $$ = new node::Node( PRINT, 1, $2 ); }
    | VARIABLE '=' expr ';'             { $$ = new node::Node( '=', 2, new node::Node( (char)$1 ), $3 ); }
    | WHILE '(' expr ')' stmt           { $$ = new node::Node( WHILE, 2, $3, $5 ); }
    | IF '(' expr ')' stmt %prec IFX    { $$ = new node::Node( IF, 2, $3, $5 ); }
    | IF '(' expr ')' stmt ELSE stmt    { $$ = new node::Node( IF, 3, $3, $5, $7 ); }
    | '{' stmt_list '}'                 { $$ = $2; }
    ;

stmt_list
    : stmt              { $$ = $1; }
    | stmt_list stmt    { $$ = new node::Node( ';', 2, $1, $2 ); }
    ;

expr
    : INTEGER               { $$ = new node::Node( $1 ); }
    | VARIABLE              { $$ = new node::Node( (char)$1 ); }
    | '-' expr %prec UMINUS { $$ = new node::Node( UMINUS, 1, $2 ); }
    | expr '+' expr         { $$ = new node::Node( '+', 2, $1, $3 ); }
    | expr '-' expr         { $$ = new node::Node( '-', 2, $1, $3 ); }
    | expr '*' expr         { $$ = new node::Node( '*', 2, $1, $3 ); }
    | expr '/' expr         { $$ = new node::Node( '/', 2, $1, $3 ); }
    | expr '>' expr         { $$ = new node::Node( '>', 2, $1, $3 ); }
    | expr '<' expr         { $$ = new node::Node( '<', 2, $1, $3 ); }
    | expr GE expr          { $$ = new node::Node( GE, 2, $1, $3 ); }
    | expr LE expr          { $$ = new node::Node( LE, 2, $1, $3 ); }
    | expr EQ expr          { $$ = new node::Node( EQ, 2, $1, $3 ); }
    | expr NE expr          { $$ = new node::Node( NE, 2, $1, $3 ); }
    | '(' expr ')'          { $$ = $2; }
    ;

%%

void cpps_error( YYLTYPE* location, LexerContext* context, const char* message ){
    std::cout << message << std::endl;
}
